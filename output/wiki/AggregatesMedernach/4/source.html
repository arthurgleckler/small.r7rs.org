
<html>
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <!-- Copyright 2013 by Arthur A. Gleckler. All rights reserved. -->
    <title>Source for wiki AggregatesMedernach version 4</title>
    <link href="/trac.css" rel="stylesheet" type="text/css"></head>
  <body>
    <header>This site is a static rendering of the <a href="https://trac.edgewall.org/">Trac</a> instance that was used by <a href="http://scheme-reports.org/2015/working-group-1.html">R7RS-WG1</a> for its work on R7RS-small (<a href="/attachment/r7rs.pdf">PDF</a>), which was ratified in 2013. For more information, see <a href="/">Home</a>.</header>
    <ul class="navigation">
      <li><a href="/">Home</a></li>
      <li><a href="/ticket/">All tickets</a></li>
      <li><a href="/wiki/">All wiki pages</a></li></ul>
    <h1>Source for wiki AggregatesMedernach version 4</h1>
    <h2>author</h2>
    <pre>medernac</pre>
    <h2>comment</h2>
    <pre>Thanks to all reviewers for their valuable comments</pre>
    <h2>ipnr</h2>
    <pre>88.177.167.25</pre>
    <h2>name</h2>
    <pre>AggregatesMedernach</pre>
    <h2>readonly</h2>
    <pre>0</pre>
    <h2>text</h2>
    <pre>
= AggregatesMedernach =

== Rationale [see 1] ==

Declaring and generating fixed sized data type disjoint from all other types, called AGGREGATES.

General other record or object features may be build on top of these aggregates.

== Datatype and associated functions [see 3] ==

(make-datatype designation fieldname ...)

Returns a new datatype. fieldname must be symbols. This is not a functional call in order to avoid potential collisions between similar aggregates but of different kind.

(datatype-&gt;designation &lt;my-datatype&gt;)

If &lt;my-datatype&gt; is of datatype kind, then returns its designation.

(datatype-&gt;fields &lt;my-datatype&gt;)

If &lt;my-datatype&gt; is of datatype kind, then returns corresponding list of fields.


== Aggregates ( constructor / accessor ) constructor ==

`(create-aggregate `''&lt;datatype&gt;''`)`

If ''&lt;datatype&gt;'' is of datatype kind, then returns corresponding constructor and switch function (see below). ''&lt;datatype&gt;'' ensures that the call to create-aggregate is functional, the same aggregate functions are generated if applied to the same ''&lt;datatype&gt;''. 

''create-aggregate'' returns 2 functions as values:

  ''make-&lt;aggregate&gt;'' is a function taking a fixed number of arguments as specified by the ''&lt;datatype&gt;'' and returning a new aggregate containing these arguments as aggregate components.

  ''&lt;aggregate&gt;-switch'' is explained below.

  The data created by make-&lt;aggregate&gt; and selected by &lt;aggregate&gt;-switch are required to conform to &lt;my-datatype&gt; properties (currently only arity).
  

== Accessing aggregate components == 

Either
 1. Unsafe access procedures must be invoked after a predicate checking data type
 2. Or safe access procedure, then a check is performed before accessing and an error is signalled if the data type is not what is expected.

However data are aggregated in order to retrieve many part of it and not only one. Solution 2 requires to perform redundant check for each accessed field and moreover the error in general is fatal to the program execution. Solution 1 alone is unsatisfactory as if an unsafe access procedure is applied to not of the correct kind data then random and unwanted behaviors may appear.

Another solution is to group together data type checking with accessing in a case analysis function (per aggregate types) :

  `(&lt;aggregate&gt;-switch `''&lt;aggregate-case&gt;''` `''&lt;else-case&gt;''`) = (lambda (`''&lt;obj&gt;''`) ...)`

Two cases are possible: if the data ''&lt;obj&gt;'' is of ''&lt;aggregate&gt;'' corresponding datatype kind then ''&lt;aggregate-case&gt;'' function is called with the components of the ''&lt;obj&gt;'' data, else ''&lt;else-case&gt;'' is called with ''&lt;obj&gt;''.

The idea behind my-datatype-switch function is to open an environment with bindings for corresponding aggregate components.


== Variants ==

A data is a variant if it is one kind of a list of aggregates.

For instance, one could view the following classic types as variant:

- List as variant of NULL or CONS

- Tree as variant of LEAF or NODE

Convenience macro for variants may be provided as in [see 2]:
{{{
(define-syntax variant-case
  (syntax-rules (else)
    ((variant-case &lt;obj&gt;)
     (error &quot;variant-case: all case exhausted &quot; &lt;obj&gt;))
    
    ((variant-case &lt;obj&gt;
        (else &lt;body&gt; ...))
     (begin &lt;body&gt; ...))
    
    ((variant-case &lt;obj&gt;
        (&lt;aggregate-switch&gt; (&lt;var&gt; ...) &lt;body&gt; ...)
        rest ...)
     ((&lt;aggregate-switch&gt;
       (lambda (&lt;var&gt; ...) &lt;body&gt; ...)
       (lambda (&lt;obj&gt;) (variant-case &lt;obj&gt; rest ...)))
      &lt;obj&gt;))))
}}}
  

== Various examples ==

{{{
(define-values (make-null null-switch)
  (create-aggregate (make-datatype &quot;NULL&quot;)))

(define-values (make-pair pair-switch)
  (create-aggregate (make-datatype &quot;PAIR&quot; 'first 'second)))

;; With my-car, my-cdr for instance:

  (define (my-car obj)
    ((pair-switch (lambda (first second) first) error)
     obj))
  
  (define (my-cdr obj)
    ((pair-switch (lambda (first second) second) error)
     obj))
}}}     

{{{
;; MAYBE data kind maker: either empty or containing some data.
(define-values (make-empty maybe-switch)
  (create-aggregate (make-datatype &quot;Empty&quot;)))

(maybe-switch &lt;empty-case&gt; &lt;not-empty-case&gt;)
}}}
{{{
;; 3d point example
(define-values (make-point3d point3d-switch)
  (create-aggregate (make-datatype &quot;3d point&quot; 'X 'Y 'Z)))

(define (point3d-length x y z)
  (sqrt (+ (* x x) (* y y) (* z z))))

(define (point3d-scale alpha)
  (lambda (x y z)
    (make-point3d (* alpha x) (* alpha y) (* alpha z))))

(define p3d (make-point3d 3 4 5))

((point3d-switch point3d-length error) p3d) ;; 7.07...
((point3d-switch (point3d-scale -2) error) p3d) ;; [-6 -8 -10]
}}}
{{{
;; Binary tree example
(define-values (make-bin-leaf bin-leaf-switch)
  (create-aggregate (make-datatype &quot;Binary tree leaf&quot; 'Data)))

(define-values (make-bin-node bin-node-switch)
  (create-aggregate (make-datatype &quot;Binary tree node&quot; 'Data 'Left 'Right)))

; variant-case example
(define (map-tree fun bin-tree)
  (variant-case bin-tree
    (bin-node-switch (data left right)
      (make-bin-node (fun data)
                     (map-tree fun left)
                     (map-tree fun right)))
    (bin-leaf-switch (data)
      (make-bin-leaf (fun data)))
    (else (error &quot;Not a bin-tree: &quot; bin-tree))))
}}}  
{{{
;; aggregate copy
(define my-aggregate-copy
  (&lt;my-aggregate-switch&gt; &lt;make-my-aggregate&gt; error))
}}}
{{{  
;; aggregate to association list converter
(define my-aggregate-&gt;alist
  (&lt;my-aggregate-switch&gt;
     (lambda l (map list (datatype-&gt;fields &lt;datatype&gt;) l))
     error))
}}}
{{{     
;; Unforgeable aggregate with built-in assertion checking.
(define-syntax create-aggregate-with-assertion
  (syntax-rules ()
    ((create-aggregate-with-assertion (&lt;datatype-designation&gt; '&lt;field&gt; ...) &lt;assertion&gt;)
     (let-values ((maker switch)
                  (create-aggregate (make-datatype (list &lt;datatype-designation&gt;) '&lt;field&gt; ...)))
       (values (lambda (&lt;field&gt; ...)
                 (if (&lt;assertion&gt; &lt;field&gt; ...)
                     (maker &lt;field&gt; ...)
                     (error &quot;Assertion failed: &quot;
                            (list &lt;datatype-designation&gt; &lt;field&gt; ...))))
               switch)))))
}}}

== Issues ==

* Maybe it is better to have instead of create-aggregate a syntactic construct to bind the created functions name ?

* An orthogonal mechanism is foreseen to allow securing data (like a lock mechanism with capabilities) but this is not the subject of this page.

== References ==

Disjointness issue raised and proposal:

[1]  Jonathan A. Rees. &quot;User-defined data types&quot;. Lisp Pointers. 'The Scheme of Things' (column). 1993 

For variant-case to destructure records:

[2]  Daniel P. Friedman, Mitchell Wand, and Christopher T. Haynes. Essentials of Programming Languages. MIT Press and McGraw-Hill, 1991.

RTD (datatype) functions:

[3]  Jonathan A. Rees, Norman I. Adams IV and James R. Meehan. &quot;The T manual&quot;. Yale University Computer Science Department. 1984.
 </pre>
    <h2>time</h2>
    <pre>2010-12-13 00:23:38</pre>
    <h2>version</h2>
    <pre>4</pre></body></html>